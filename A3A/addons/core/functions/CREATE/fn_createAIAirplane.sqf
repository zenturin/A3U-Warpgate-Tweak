#include "..\..\script_component.hpp"
FIX_LINE_NUMBERS()
if (!isServer and hasInterface) exitWith{};

params ["_markerX"];

//Not sure if that ever happens, but it reduces redundance
if(spawner getVariable _markerX == 2) exitWith {};

ServerDebug_1("Spawning Airbase %1", _markerX);

private _vehiclesX = [];
private _groups = [];
private _soldiers = [];
private _props = [];
private _dogs = []; //dogs are used in fn_location_createPatrols, removing this variable will break spawn

private _positionX = getMarkerPos (_markerX);

private _size = [_markerX] call A3A_fnc_sizeMarker;
//_garrison = garrison getVariable _markerX;

private _frontierX = [_markerX] call A3A_fnc_isFrontline;
private _busy = if (dateToNumber date > server getVariable _markerX) then {false} else {true};
private _nVeh = round (_size/60);

private _sideX = sidesX getVariable [_markerX,sideUnknown];
private _faction = Faction(_sideX);

/////////////////////////////
// SPAWNING AA ELEMENTS	  //
////////////////////////////
private _radarType = _faction getOrDefault ["vehicleRadar", ""];
private _samType = _faction getOrDefault ["vehicleSam", ""];

if (_radarType != "" && {_samType != ""}) then {
	private _spawnParameter = [_markerX, "Sam"] call A3A_fnc_findSpawnPosition;
	while {_spawnParameter isEqualType []} do {
		{
			private _aaVehicle = [_x, _spawnParameter select 0, 25, 10, true] call A3A_fnc_safeVehicleSpawn;
			private _crewType = [_sideX, _aaVehicle] call A3A_fnc_crewTypeForVehicle;
			private _aaGroup = createGroup _sideX;

			_aaGroup = [_aaGroup, _aaVehicle, _crewType] call A3A_fnc_createVehicleCrew;

			[_aaVehicle, _sideX] call A3A_fnc_AIVEHinit;

			_soldiers append (units _aaGroup);
            _groups pushBack _aaGroup;
            _vehiclesX pushBack _aaVehicle;		

			//radar rotation
            if(_x isEqualTo _radarType) then {
                _aaVehicle spawn {
                    while {alive _this} do {
                        {
                            _this lookAt (_this getRelPos [100, _x]);
                            sleep 2.45;
                        } forEach [120, 240, 0];
                    };
                };
            };	
		} forEach [_radarType, _samType];
		_spawnParameter = [_markerX, "Sam"] call A3A_fnc_findSpawnPosition;
	};
};

/////////////////////////////
// Self-propelled AA 	  //
////////////////////////////

private _typeVehX = selectRandom (_faction get "vehiclesAA");
private _max = if (_frontierX && {[_typeVehX] call A3A_fnc_vehAvailable}) then {2} else {1};
for "_i" from 1 to _max do {
	private _spawnParameter = [_markerX, "Vehicle"] call A3A_fnc_findSpawnPosition;

	if (_spawnParameter isEqualType []) then
	{
		private _vehicle=[_spawnParameter select 0, _spawnParameter select 1,_typeVehX, _sideX] call A3A_fnc_spawnVehicle;
		private _veh = _vehicle select 0;
		private _vehCrew = _vehicle select 1;
		{[_x,_markerX] call A3A_fnc_NATOinit} forEach _vehCrew;
		[_veh, _sideX] call A3A_fnc_AIVEHinit;
		private _groupVeh = _vehicle select 2;

		_soldiers append _vehCrew;
		_groups pushBack _groupVeh;
		_vehiclesX pushBack _veh;

		sleep 1;
		[(gunner _veh), 300] spawn SCRT_fnc_common_scanHorizon;
	}
	else
	{
		_i = _max;
	};
};

/////////////////////////////
// Heavy Patrol Vehicle   //
////////////////////////////

if (_frontierX && {random 100 < (20 + tierWar * 3)}) then {
	private _heavyVehPool =  (_faction get "vehiclesTanks") + (_faction get "vehiclesAPCs") + (_faction get "vehiclesIFVs");
	_heavyVehPool = _heavyVehPool call BIS_fnc_arrayShuffle;

	private _availableIndex = _heavyVehPool findIf {[_x] call A3A_fnc_vehAvailable};
	if (_availableIndex == -1) exitWith {};

	private _road = [_positionX] call A3A_fnc_findNearestGoodRoad;
	if (_road distance2D _positionX > 800) exitWith {};

	private _type = _heavyVehPool select _availableIndex;

	private _heavyVehicle = [(_heavyVehPool select _availableIndex), (position _road), 15, 10] call A3A_fnc_safeVehicleSpawn;
	if (isNull _heavyVehicle) exitWith {};

	private _crewType = [_sideX, _heavyVehicle] call A3A_fnc_crewTypeForVehicle;
	private _group = createGroup _sideX;

	_group = [_group, _heavyVehicle, _crewType] call A3A_fnc_createVehicleCrew;

	[_heavyVehicle, _sideX] call A3A_fnc_AIVEHinit;
	{[_x,_markerX] call A3A_fnc_NATOinit} forEach (units _group);

	if (_type in ((_faction get "vehiclesAPCs") + (_faction get "vehiclesIFVs"))) then {
		sleep 1;
		private _troopGroup = [(position _road), _sideX, (selectRandom ([_faction, "groupsTierMedium"] call SCRT_fnc_unit_flattenTier))] call A3A_fnc_spawnGroup;
		{_x assignAsCargo _heavyVehicle;_x moveInCargo _heavyVehicle; _soldiers pushBack _x; [_x] joinSilent _group; [_x,"",false] call A3A_fnc_NATOinit} forEach units _troopGroup;
		deleteGroup _troopGroup;
	};

	_soldiers append (units _group);
	_groups pushBack _group;
	_vehiclesX pushBack _heavyVehicle;		
};

if (_frontierX) then {
	private _roads = _positionX nearRoads _size;
	if (count _roads != 0) then {
		private _groupX = createGroup _sideX;
		_groups pushBack _groupX;

		private _dist = 0;
		private _road = objNull;
		{if ((position _x) distance _positionX > _dist) then {_road = _x;_dist = position _x distance _positionX}} forEach _roads;
		private _roadscon = roadsConnectedto _road;

		private _roadcon = objNull;
		{if ((position _x) distance _positionX > _dist) then {_roadcon = _x}} forEach _roadscon;

		private _dirveh = [_roadcon, _road] call BIS_fnc_dirTo;
		private _pos = [getPos _road, 7, _dirveh + 270] call BIS_Fnc_relPos;
		private _bunker = (_faction get "sandbag") createVehicle _pos;

		_vehiclesX pushBack _bunker;

		_bunker setDir _dirveh;
		_pos = getPosATL _bunker;

		private _typeVehX = selectRandom (_faction get "staticATs");
		private _veh = _typeVehX createVehicle _positionX;

		_vehiclesX pushBack _veh;

		_veh setDir _dirVeh + 180;
		_veh setPos _pos;

		private _typeUnit = [_faction get "unitTierStaticCrew"] call SCRT_fnc_unit_getTiered;
		private _unit = [_groupX, _typeUnit, _positionX, [], 0, "NONE"] call A3A_fnc_createUnit;
		[_unit,_markerX] call A3A_fnc_NATOinit;
		[_veh, _sideX] call A3A_fnc_AIVEHinit;
		_unit moveInGunner _veh;
		_soldiers pushBack _unit;
	};
};


private _mrk = createMarkerLocal [format ["%1patrolarea", random 100], _positionX];
_mrk setMarkerShapeLocal "RECTANGLE";
_mrk setMarkerSizeLocal [(distanceSPWN/2),(distanceSPWN/2)];
_mrk setMarkerTypeLocal "hd_warning";
_mrk setMarkerColorLocal "ColorRed";
_mrk setMarkerBrushLocal "DiagGrid";
private _ang = markerDir _markerX;
_mrk setMarkerDirLocal _ang;
if (!debug) then {_mrk setMarkerAlphaLocal 0};

//maybe it's no longer needed after all..?
private _additionalGarrison = [_sideX, _markerX] call SCRT_fnc_garrison_rollOversizeGarrison;
if (_additionalGarrison isNotEqualTo []) then {
	for "_i" from 0 to (count _additionalGarrison) - 1 do {
		private _groupTypes = _additionalGarrison select _i;
		private _group = [_positionX, _sideX, _groupTypes, false, true] call A3A_fnc_spawnGroup;
		if !(isNull _group) then {
			sleep 1;
			[leader _group, _mrk, "SAFE","SPAWNED", "RANDOM", "NOVEH2"] call A3A_fnc_proxyUPSMON;//TODO need delete UPSMON link
			_groups pushBack _group;
			{[_x] call A3A_fnc_NATOinit; _soldiers pushBack _x} forEach units _group;
		};
	};
};

private _garrison = garrison getVariable [_markerX,[]];
_garrison = _garrison call A3A_fnc_garrisonReorg;

private _radiusX = count _garrison;
private _patrol = true;

if (_radiusX < ([_markerX] call A3A_fnc_garrisonSize)) then {
	_patrol = false;
} else {
	//No patrol if patrol area overlaps with an enemy site
	_patrol = ((markersX findIf {(getMarkerPos _x inArea _mrk) && {sidesX getVariable [_x, sideUnknown] != _sideX}}) == -1);
};

if (_patrol) then {
	[_markerX, _positionX, _sideX, _faction, 5] call SCRT_fnc_location_createPatrols;
};
private _countX = 0;

// Mortar spawning
private _groupX = createGroup _sideX;
_groups pushBack _groupX;
private _typeUnit = [_faction get "unitTierStaticCrew"] call SCRT_fnc_unit_getTiered;
while {true} do {
	private _spawnParameter = [_markerX, "Mortar"] call A3A_fnc_findSpawnPosition;
	if (_spawnParameter isEqualType 0) exitWith {};

	private _mortarPos = _spawnParameter select 0;
	if (_mortarPos nearObjects ["StaticWeapon", 5] isNotEqualTo []) then { continue };		// hack, already a (support?) mortar on the point

	private _typeVehX = selectRandom (_faction get "staticMortars");
	private _veh = _typeVehX createVehicle _mortarPos;
	_veh setDir (_spawnParameter select 1);
	_nul=[_veh] execVM QPATHTOFOLDER(scripts\UPSMON\MON_artillery_add.sqf);//TODO need delete UPSMON link
	private _unit = [_groupX, _typeUnit, _positionX, [], 0, "CAN_COLLIDE"] call A3A_fnc_createUnit;
	[_unit,_markerX] call A3A_fnc_NATOinit;

	_unit moveInGunner _veh;

	_soldiers pushBack _unit;
	_vehiclesX pushBack _veh;

	[_veh, _sideX] call A3A_fnc_AIVEHinit;
	sleep 1;

	{
		private _relativePosition = [_mortarPos, 4, _x] call BIS_fnc_relPos;
		private _sandbag = createVehicle [_faction get "sandbagRound", _relativePosition, [], 0, "CAN_COLLIDE"];
		_sandbag setDir ([_sandbag, _mortarPos] call BIS_fnc_dirTo);
		_sandbag setVectorUp surfaceNormal position _sandbag;
		_props pushBack _sandbag;
	} forEach [0, 90, 180, 270];
};

private _ret = [_markerX,_size,_sideX,_frontierX] call A3A_fnc_milBuildings;

_groups pushBack (_ret select 0);
_vehiclesX append (_ret select 1);
_soldiers append (_ret select 2);

{[_x, _sideX] call A3A_fnc_AIVEHinit} forEach (_ret select 1);

if(random 100 < (50 + tierWar * 3)) then {
	_large = (random 100 < (40 + tierWar * 2));
	[_markerX, _large] spawn A3A_fnc_placeIntel;
};

if (!_busy) then {
	//Newer system in place
	private _runwaySpawnLocation = [_markerX] call A3A_fnc_getRunwayTakeoffForAirportMarker;
	private _spawnParameter = [_markerX, "Plane"] call A3A_fnc_findSpawnPosition;
	if !(_runwaySpawnLocation isEqualTo []) then
	{
		_pos = _runwaySpawnLocation select 0;
		_ang = _runwaySpawnLocation select 1;
	};
	private _groupX = createGroup _sideX;
	_groups pushBack _groupX;
	_countX = 0;
	while {_countX < 3} do
	{
		private _veh = objNull;
		if(_spawnParameter isEqualType []) then
		{
			private _vehPool = ((_faction get "vehiclesPlanesCAS") + (_faction get "vehiclesPlanesAA")) select {[_x] call A3A_fnc_vehAvailable};
			if(count _vehPool > 0) then
			{
				_typeVehX = selectRandom _vehPool;
				_veh = createVehicle [_typeVehX, (_spawnParameter select 0), [], 0, "CAN_COLLIDE"];
				_veh setDir (_spawnParameter select 1);
				_veh setPos (_spawnParameter select 0);
				_vehiclesX pushBack _veh;
				[_veh, _sideX] call A3A_fnc_AIVEHinit;
			};
			_spawnParameter = [_markerX, "Plane"] call A3A_fnc_findSpawnPosition;
		}
		else
		{
			if !(_runwaySpawnLocation isEqualTo []) then
			{
				_typeVehX = selectRandom ((
                    (_faction get "vehiclesHelisLight")
                    + (_faction get "vehiclesHelisTransport")
                    + (_faction get "vehiclesPlanesCAS")
                    + (_faction get "vehiclesPlanesAA")
                    + (_faction get "vehiclesPlanesTransport")
                ) select {[_x] call A3A_fnc_vehAvailable});
				_veh = createVehicle [_typeVehX, _pos, [],3, "NONE"];
				_veh setDir (_ang);
				_pos = [_pos, 50,_ang] call BIS_fnc_relPos;
				_vehiclesX pushBack _veh;
				[_veh, _sideX] call A3A_fnc_AIVEHinit;
			}
			else
			{
				//No places found, neither hangar nor runway
				_countX = 3;
			};
		};
		_countX = _countX + 1;
	};
};

private _typeVehX = _faction get "flag";
private _flagX = createVehicle [_typeVehX, _positionX, [],0, "NONE"];

_flagX allowDamage false;
[_flagX,"take"] remoteExec ["A3A_fnc_flagaction",[teamPlayer,civilian],_flagX];

_vehiclesX pushBack _flagX;
if (flagTexture _flagX != (_faction get "flagTexture")) then {[_flagX,(_faction get "flagTexture")] remoteExec ["setFlagTexture",_flagX]};

// Only create ammoBox if it's been recharged (see reinforcementsAI)
private _ammoBox = if (garrison getVariable [_markerX + "_lootCD", 0] == 0) then
{
	private _ammoBoxType = _faction get "ammobox";
	private _ammoBox = [_ammoBoxType, _positionX, 15, 5, true] call A3A_fnc_safeVehicleSpawn;
	// Otherwise when destroyed, ammoboxes sink 100m underground and are never cleared up
	_ammoBox addEventHandler ["Killed", { [_this#0] spawn { sleep 10; deleteVehicle (_this#0) } }];
	[_ammoBox] spawn A3A_fnc_fillLootCrate;
	[_ammoBox] call A3A_Logistics_fnc_addLoadAction;

	[_ammoBox] spawn {
		sleep 1;    //make sure fillLootCrate finished clearing the crate
		{
			_this#0 addItemCargoGlobal [_x, round random [5,15,15]];
		} forEach (A3A_faction_reb get "flyGear");
	};
	_ammoBox;
};


if (!_busy) then
{
	for "_i" from 1 to (round (random 2)) do
	{
		private _arrayVehAAF = ((_faction get "vehiclesAPCs") + (_faction get "vehiclesTanks") + (_faction get "vehiclesLightTanks")) select {[_x] call A3A_fnc_vehAvailable};
		private _spawnParameter = [_markerX, "Vehicle"] call A3A_fnc_findSpawnPosition;
		if (count _arrayVehAAF > 0 && {_spawnParameter isEqualType []}) then
		{
			_veh = createVehicle [selectRandom _arrayVehAAF, (_spawnParameter select 0), [], 0, "CAN_COLLIDE"];
			_veh setDir (_spawnParameter select 1);
			_vehiclesX pushBack _veh;
			[_veh, _sideX] call A3A_fnc_AIVEHinit;
			_nVeh = _nVeh -1;
			sleep 1;
		};
	};
};

private _arrayVehAAF = (_faction get "vehiclesLightArmed") + (_faction get "vehiclesLightUnarmed") + (_faction get "vehiclesTrucks") + (_faction get "vehiclesAmmoTrucks") + (_faction get "vehiclesRepairTrucks") + (_faction get "vehiclesFuelTrucks") + (_faction get "vehiclesMedical");
_countX = 0;

while {_countX < _nVeh && {_countX < 3}} do
{
	private _typeVehX = selectRandom _arrayVehAAF;
	private _spawnParameter = [_markerX, "Vehicle"] call A3A_fnc_findSpawnPosition;
	if(_spawnParameter isEqualType []) then
	{
		_veh = createVehicle [_typeVehX, (_spawnParameter select 0), [], 0, "NONE"];
		_veh setDir (_spawnParameter select 1);
		_vehiclesX pushBack _veh;
		[_veh, _sideX] call A3A_fnc_AIVEHinit;
		sleep 1;
		_countX = _countX + 1;
	}
	else
	{
		//No further spaces to spawn vehicle
		_countX = _nVeh;
	};
};

{ _x setVariable ["originalPos", getPos _x] } forEach _vehiclesX;

private _array = [];
private _subArray = [];
_countX = 0;
_radiusX = _radiusX -1;
while {_countX <= _radiusX} do {
	_array pushBack (_garrison select [_countX,7]);
	_countX = _countX + 8;
};
for "_i" from 0 to (count _array - 1) do {
	_groupX = if (_i == 0) then {[_positionX,_sideX, (_array select _i),true,false] call A3A_fnc_spawnGroup} else {[_positionX,_sideX, (_array select _i),false,true] call A3A_fnc_spawnGroup};
	_groups pushBack _groupX;
	{[_x,_markerX] call A3A_fnc_NATOinit; _soldiers pushBack _x} forEach units _groupX;
	if (_i == 0) then {_nul = [leader _groupX, _markerX, "LIMITED", "SAFE", "RANDOMUP","SPAWNED", "NOVEH2", "NOFOLLOW"] call A3A_fnc_proxyUPSMON} else {_nul = [leader _groupX, _markerX, "LIMITED", "SAFE","SPAWNED", "RANDOM","NOVEH2", "NOFOLLOW"] call A3A_fnc_proxyUPSMON};
};//TODO need delete UPSMON link

["locationSpawned", [_markerX, "Airport", true]] call EFUNC(Events,triggerEvent);


waitUntil {sleep 1; (spawner getVariable _markerX == 2)};

[_markerX] call A3A_fnc_freeSpawnPositions;

deleteMarker _mrk;
{ if (alive _x) then { deleteVehicle _x } } forEach _soldiers;
{ deleteGroup _x } forEach _groups;

{
	// delete all vehicles that haven't been stolen
	if (_x getVariable ["ownerSide", _sideX] == _sideX) then {
		if (_x distance2d (_x getVariable "originalPos") < 100) then { deleteVehicle _x }
		else { if !(_x isKindOf "StaticWeapon") then { [_x] spawn A3A_fnc_VEHdespawner } };
	};
} forEach _vehiclesX;

{ deleteVehicle _x } forEach _props;
{ deleteVehicle _x } forEach _dogs;

// If loot crate was stolen, set the cooldown
if (!isNil "_ammoBox") then {
	if ((alive _ammoBox) and (_ammoBox distance2d _positionX < 100)) exitWith { deleteVehicle _ammoBox };
	if (alive _ammoBox) then { [_ammoBox] spawn A3A_fnc_VEHdespawner };
	private _lootCD = 120*16 / ([_markerX] call A3A_fnc_garrisonSize);
	garrison setVariable [_markerX + "_lootCD", _lootCD, true];
};
["locationSpawned", [_markerX, "Airport", false]] call EFUNC(Events,triggerEvent);
