/*
Author: HÃ¥kon
Description:
    Generates a list off all assets used by the factions, replaces old global variables like vehAttack

Arguments: <Nil>

Return Value: <Hashmap> the hashmap A3A_faction_all

Scope: Server
Environment: unscheduled
Public: No
Dependencies:

Example:

License: MIT License
*/
#include "..\..\script_component.hpp"
FIX_LINE_NUMBERS()

#define OccAndInv(VAR) (FactionGetOrDefault(occ, VAR, []) + FactionGetOrDefault(inv, VAR, []))
#define Reb(VAR) FactionGetOrDefault(reb, VAR, [])
#define Riv(VAR) FactionGetOrDefault(riv, VAR, [])
#define Occ(VAR) FactionGetOrDefault(occ, VAR, [])
#define Inv(VAR) FactionGetOrDefault(inv, VAR, [])

A3A_faction_all = createHashMap;
//setVar expects an array
#define setVar(VAR, VALUE) A3A_faction_all set [VAR, (VALUE) arrayIntersect (VALUE)];
#define getVar(VAR) (A3A_faction_all get VAR)


private _fnc_extractMarketClasses = {
    private _type = _this;
    private _vehicleRegisters = (A3A_faction_reb get "blackMarketStock") select {(_x select 2) isEqualTo _type};
    if (_vehicleRegisters isEqualTo []) exitWith {[]};

    _vehicleRegisters apply {_x select 0}
};

  //=====\\
 // Units \\
// ======= \\
Info("Identifying unit types");

//create empty lists
private _squadLeaders = [];
private _medics = [];
private _radioMen = [];

//add occ and inv units to the lists
{
    private _prefix = _x;
    {
        _squadLeaders pushBack ('loadouts_'+_prefix+_x+'SquadLeader');//type
        _medics pushBack ('loadouts_'+_prefix+_x+'Medic');
        _radioMen pushBack ('loadouts_'+_prefix+_x+'Radioman');
    } forEach ["militia_","military_","elite_","SF_"];//section
} forEach ["occ_", "inv_"];//prefix

_squadLeaders append [
    'loadouts_occ_other_Official',
    'loadouts_riv_militia_CellLeader',
    'loadouts_riv_militia_Commander',
    'loadouts_reb_militia_SquadLeader'
];
_medics append [
    'loadouts_reb_militia_Medic',
    'loadouts_riv_militia_Medic'
];

_radioMen pushBack 'loadouts_occ_other_Official';

//set the lists in the hm
setVar("SquadLeaders", _squadLeaders);
setVar("Medics", _medics);
setVar("Radiomen", _radioMen);

  //========\\
 // Vehicles \\
// ========== \\
Info("Identifying vehicle types");

//Occ&Inv X vehicles
setVar("vehiclesPolice", OccAndInv("vehiclesPolice"));
setVar("vehiclesAttack", OccAndInv("vehiclesAttack"));
setVar("vehiclesAmmoTrucks", OccAndInv("vehiclesAmmoTrucks"));
setVar("vehiclesLightAPCs", OccAndInv("vehiclesLightAPCs"));
setVar("vehiclesMedical", OccAndInv("vehiclesMedical") + ((A3A_faction_civ get "vehiclesCivMedical") select {_x isEqualType ""}));
setVar("vehiclesAPCs", OccAndInv("vehiclesAPCs") + Riv("vehiclesRivalsAPCs") + ("APC" call _fnc_extractMarketClasses));
setVar("vehiclesIFVs", OccAndInv("vehiclesIFVs") );
setVar("vehiclesUAVs", OccAndInv("uavsAttack")+ OccAndInv("uavsPortable") + Riv("vehiclesRivalsUavs") + ("UAV" call _fnc_extractMarketClasses));
setVar("vehiclesAA", OccAndInv("vehiclesAA") + ("AA" call _fnc_extractMarketClasses) + [Reb("vehicleAA")] - [""]);
setVar("vehiclesArtillery", OccAndInv("vehiclesArtillery") + ("ARTILLERY" call _fnc_extractMarketClasses));
setVar("vehiclesTanks", OccAndInv("vehiclesTanks") + Riv("vehiclesRivalsTanks") + ("TANK" call _fnc_extractMarketClasses));
setVar("vehiclesTransportAir", OccAndInv("vehiclesHelisLight") + OccAndInv("vehiclesHelisTransport") + OccAndInv("vehiclesPlanesTransport"));
setVar("vehiclesHelisLight", OccAndInv("vehiclesHelisLight") );
setVar("vehiclesHelisLightAttack", OccAndInv("vehiclesHelisLightAttack") );
setVar("vehiclesHelisAttack", OccAndInv("vehiclesHelisAttack") );
setVar("vehiclesHelisTransport", OccAndInv("vehiclesHelisTransport") );
setVar("vehiclesPlanesAA", OccAndInv("vehiclesPlanesAA") );
setVar("vehiclesPlanesCAS", OccAndInv("vehiclesPlanesCAS") );
setVar("vehiclesPlanesTransport", OccAndInv("vehiclesPlanesTransport"));
setVar("staticMortars", OccAndInv("staticMortars") + Riv("staticMortars") + [Reb("staticMortar")]);
setVar("staticAA", OccAndInv("staticAAs") + Riv("staticAAs") + [Reb("staticAA")] + ("STATICAA" call _fnc_extractMarketClasses));
setVar("staticAT", OccAndInv("staticATs") + Riv("staticATs") + [Reb("staticAT")] + ("STATICAT" call _fnc_extractMarketClasses));
setVar("staticMG", OccAndInv("staticMGs") + Riv("staticMGs") + [Reb("staticMG")] + ("STATICMG" call _fnc_extractMarketClasses));

//Antistasi Plus stuff
setVar("vehiclesAirborne", OccAndInv("vehiclesAirborne"));
setVar("vehiclesLightTanks", OccAndInv("vehiclesLightTanks"));

setVar("vehicleRadars", [Occ("vehicleRadar"), Inv("vehicleRadar")]);
setVar("vehicleSams", [Occ("vehicleSam"), Inv("vehicleSam")]);
setVar("staticHowitzers", OccAndInv("staticHowitzers"));

//for some reason it doesn't want to merge/append/concat animation source arrays
private _animationMap = createHashMap;
{_animationMap set [_x select 0, _x select 1]} forEach (A3A_faction_inv get "animations");
{_animationMap set [_x select 0, _x select 1]} forEach (A3A_faction_occ get "animations");
{_animationMap set [_x select 0, _x select 1]} forEach (A3A_faction_reb get "animations");
{_animationMap set [_x select 0, _x select 1]} forEach (A3A_faction_riv get "animations");
A3A_faction_all set ["animations", _animationMap];

private _variantMap = createHashMap;
{_variantMap set [_x select 0, _x select 1]} forEach (A3A_faction_inv get "variants");
{_variantMap set [_x select 0, _x select 1]} forEach (A3A_faction_occ get "variants");
{_variantMap set [_x select 0, _x select 1]} forEach (A3A_faction_reb get "variants");
{_variantMap set [_x select 0, _x select 1]} forEach (A3A_faction_riv get "variants");
A3A_faction_all set ["variants", _variantMap];

//Rivals
private _vehRivalsArmor = Riv("vehiclesRivalsAPCs") + Riv("vehiclesRivalsTanks");
setVar("vehiclesRivalsArmor", _vehRivalsArmor);

private _vehRivalsLight = Riv("vehiclesRivalsCars") + Riv("vehiclesRivalsLightArmed") + Riv("vehiclesRivalsTrucks");
setVar("vehiclesRivalsLight", _vehRivalsLight);

private _vehRivalsStatics = Riv("staticMGs") + Riv("staticATs") + Riv("staticAAs");
setVar("vehiclesRivalsStatics", _vehRivalsStatics);

private _vehRivals = Riv("vehiclesRivalsAPCs") 
+ Riv("vehiclesRivalsTanks") 
+ Riv("vehiclesRivalsTrucks") 
+ Riv("vehiclesRivalsCars") 
+ Riv("vehiclesRivalsLightArmed") 
+ Riv("vehiclesRivalsUavs");
setVar("vehiclesRivals", _vehRivals);

private _vehMilitia = OccAndInv("vehiclesMilitiaCars")
+ OccAndInv("vehiclesMilitiaTrucks")
+ OccAndInv("vehiclesMilitiaLightArmed")
+ OccAndInv("vehiclesMilitiaAPCs");
setVar("vehiclesMilitia", _vehMilitia);

//boats
private _vehBoats = OccAndInv("vehiclesTransportBoats") + OccAndInv("vehiclesGunBoats") + [Reb("vehicleBoat")];
setVar("vehiclesBoats", _vehBoats);

//Occ&Inv helicopters
private _vehHelis =
OccAndInv("vehiclesHelisTransport")
+ OccAndInv("vehiclesHelisLightAttack")
+ OccAndInv("vehiclesHelisAttack")
+ OccAndInv("vehiclesHelisLight")
+ ("HELI" call _fnc_extractMarketClasses);
setVar("vehiclesHelis", _vehHelis);

//fixed winged aircrafts
private _vehFixedWing =
OccAndInv("vehiclesPlanesCAS")
+ OccAndInv("vehiclesPlanesAA")
+ OccAndInv("vehiclesPlanesTransport")
+ ("PLANE" call _fnc_extractMarketClasses)
+ [Reb("vehiclePlane"), Reb("vehiclePayloadPlane")];
setVar("vehiclesFixedWing", _vehFixedWing);

//trucks to carry infantry
private _vehTrucks =
OccAndInv("vehiclesTrucks")
+ OccAndInv("vehiclesMilitiaTrucks")
+ Riv("vehiclesRivalsTrucks")
+ [Reb("vehicleTruck")];
setVar("vehiclesTrucks", _vehTrucks);

//Armed cars
private _carsArmed =
OccAndInv("vehiclesLightArmed")
+ OccAndInv("vehiclesMilitiaLightArmed")
+ Riv("vehiclesRivalsLightArmed")
+ [Reb("vehicleLightArmed")];
setVar("vehiclesLightArmed", _carsArmed);

//Unarmed cars
private _carsUnarmed =
OccAndInv("vehiclesLightUnarmed")      // anything else?
+ OccAndInv("vehiclesMilitiaCars")
+ OccAndInv("vehiclesPolice")
+ Riv("vehiclesRivalsCars")
+ [Reb("vehicleLightUnarmed")];
setVar("vehiclesLightUnarmed", _carsUnarmed);
setVar("vehiclesLight", _carsArmed + _carsUnarmed);

//all Occ&Inv armor
private _vehArmor =
getVar("vehiclesTanks")
+ getVar("vehiclesAA")
+ getVar("vehiclesArtillery")
+ getVar("vehiclesAPCs")
+ getVar("vehiclesLightTanks")
+ getVar("vehiclesAirborne")
+ getVar("vehiclesIFVs");
setVar("vehiclesArmor", _vehArmor);

//rebel vehicles
private _vehReb = [
    Reb("vehicleBasic"), Reb("vehicleTruck"), Reb("vehicleRepair"), Reb("vehicleBoat")
    , Reb("vehicleAT"), Reb("vehicleLightArmed"), Reb("vehicleLightUnarmed")
    , Reb("staticMG"), Reb("staticAT"), Reb("staticAA"), Reb("staticMortar")
    , Reb("vehiclePlane"), Reb("vehiclePayloadPlane")
    , ((A3A_faction_reb get "blackMarketStock") apply {_x select 0})
];
setVar("vehiclesReb", _vehReb);

//trucks that can cary logistics cargo
private _vehCargoTrucks = (_vehTrucks + OccAndInv("vehiclesCargoTrucks")) select { [_x] call A3A_Logistics_fnc_getVehCapacity > 1 };
setVar("vehiclesCargoTrucks", _vehCargoTrucks);

missionNamespace setVariable ["A3A_faction_all", A3A_faction_all, true];
A3A_faction_all
